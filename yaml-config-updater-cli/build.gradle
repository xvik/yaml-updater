import java.text.SimpleDateFormat

buildscript {
    repositories { mavenCentral() }
    dependencies {
        if (JavaVersion.current() >= JavaVersion.VERSION_17) {
            classpath 'org.graalvm.buildtools:native-gradle-plugin:0.11.2'
        }
    }
}

plugins {
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

description = "Command line executor for yaml config updater"

dependencies {
    implementation project(':yaml-config-updater')
    implementation 'info.picocli:picocli:4.7.7'
    implementation 'org.slf4j:slf4j-simple:1.7.36'

    annotationProcessor 'info.picocli:picocli-codegen:4.7.7'
}

compileJava {
    options.compilerArgs += ["-Aproject=${project.group}/${project.name}",
                             "-Aother.resource.patterns=META-INF/VERSION"]
}

// custom version file is required because native image would override MANIFEST.MF
task generateVersionFile {
    inputs.property('source', "${ -> project.version} (${new SimpleDateFormat('dd MMMM YYYY', Locale.ENGLISH).format(new Date())})")
    outputs.file "$project.buildDir/version/VERSION"
    doLast {
        File file = outputs.files.singleFile
        if (file.exists()) {
            file.delete()
        } else {
            file.parentFile.mkdirs()
        }
        file << inputs.properties.get('source')
    }
}

model {
    tasks.jar {
        into("META-INF/") {
            from generateVersionFile
        }
    }
}

shadowJar {
    into("META-INF/") {
        from generateVersionFile
    }
}

jar.manifest.attributes 'Main-Class': 'ru.vyarus.yaml.updater.cli.UpdateConfigCli'

assemble.dependsOn = [shadowJar]

// Plugin requires java 17, but project targets java 8.
// Without this condition tests would complain even on java 11 and above (failed dependency resolution)
if (gradle.startParameter.taskNames.findAll { String it -> it.endsWith('nativeCompile')} .size() > 0) {
    apply plugin: 'org.graalvm.buildtools.native'

    // not used during release
    graalvmNative {
        binaries {
            main {
                imageName = project.name
                mainClass = 'ru.vyarus.yaml.updater.cli.UpdateConfigCli'
            }
            all {
                resources.autodetect()
                buildArgs.add('-Os')
            }
        }
    }
}
